
//uniform usampler2D m_BufferMap;
uvec4 pack(vec4 col, vec3 norm, float depth) {
	//10 bit color RGB, 2 bits free to be used
	//10 bit normal XYZ, 2 bits free to be used
	//last 32 bits free to be used as well

	uvec4 u = uvec4(clamp(col*1023.0, vec4(0.0), vec4(1023.0)));
	uvec3 n = uvec3(clamp((norm*0.5+0.5)*1023.0, vec3(0.0), vec3(1023.0)));
	
	return uvec4(
			((u.x << 20) | (u.y << 10) | u.z), 
			((n.x << 20) | (n.y << 10) | n.z), 
			floatBitsToUint(depth),
			0u);
}
uvec4 pack(vec4 col) {
	//10 bit color RGB, 2 bits free to be used
	//10 bit normal XYZ, 2 bits free to be used
	//last 32 bits free to be used as well

	uvec4 u = uvec4(clamp(col*1023.0, vec4(0.0), vec4(1023.0)));
	
	return uvec4(((u.x << 20) | (u.y << 10) | u.z), 
			0u, 0u,	0u);
}
vec4 getColor(uvec4 data) {
	return vec4(uvec4((data.x >> 20), (data.x >> 10), data.x, 0u) & 0x3FFu) * 0.000977517;
}
vec3 getNormal(uvec4 data) {
	return vec3(uvec3((data.y >> 20), (data.y >> 10), data.y) & 0x3FFu) * 0.001955034 - 1.0;
}
float getDepth(uvec4 data) {
	return uintBitsToFloat(data.z);
}


